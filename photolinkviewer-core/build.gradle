apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dexOptions {
        incremental true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:23.1.1"
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'org.twitter4j:twitter4j-core:4.0.4'
    compile 'org.twitter4j:twitter4j-async:4.0.4'
    compile 'org.twitter4j:twitter4j-http2-support:4.0.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile 'com.squareup.okhttp:okhttp:2.7.4'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'de.greenrobot:eventbus:2.4.0'

    androidTestCompile('com.android.support.test:runner:0.4'){
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4'){
        exclude module: 'support-annotations'
    }
}

buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

def siteUrl = 'https://github.com/nonylene/PhotoLinkViewer-Core'
def gitUrl = 'https://github.com/nonylene/PhotoLinkViewer-Core.git'

def repo = new File(rootDir, "repository")
def snapshotRepo = new File(rootDir, "repository/snapshots")

group = 'net.nonylene'
version = '0.0.3-SNAPSHOT'

apply plugin: 'maven'

uploadArchives {
    repositories.mavenDeployer {
        repository url: "file://${repo.absolutePath}"
        snapshotRepository url: "file://${snapshotRepo.absolutePath}"

        pom.artifactId = 'photolinkviewer-core'
        pom.project {
            packaging 'aar'
            name 'PhotoLinkViewer-Core'
            url siteUrl
            licenses {
                license {
                    name 'GNU GENERAL PUBLIC LICENSE Version 2'
                    url 'https://www.gnu.org/licenses/gpl-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'nonylene'
                    email 'cookie.speaker@gmail.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}
