apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 25
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding {
        enabled = true
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:25.1.0"
    compile 'com.android.support:support-v13:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'org.twitter4j:twitter4j-core:4.0.6'
    compile 'org.twitter4j:twitter4j-async:4.0.6'
    compile 'org.twitter4j:twitter4j-http2-support:4.0.6'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@jar'
    kapt "com.android.databinding:compiler:2.2.3"

    androidTestCompile('com.android.support.test:runner:0.4'){
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4'){
        exclude module: 'support-annotations'
    }
}

buildscript {
    ext.kotlin_version = '1.0.5-2'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

def siteUrl = 'https://github.com/nonylene/PhotoLinkViewer-Core'
def gitUrl = 'https://github.com/nonylene/PhotoLinkViewer-Core.git'

def repo = new File(rootDir, "repository")
def snapshotRepo = new File(rootDir, "repository/snapshots")

group = 'net.nonylene'
version = '0.0.7-SNAPSHOT'

apply plugin: 'maven'

uploadArchives {
    repositories.mavenDeployer {
        repository url: "file://${repo.absolutePath}"
        snapshotRepository url: "file://${snapshotRepo.absolutePath}"

        pom.artifactId = 'photolinkviewer-core'
        pom.project {
            packaging 'aar'
            name 'PhotoLinkViewer-Core'
            url siteUrl
            licenses {
                license {
                    name 'GNU GENERAL PUBLIC LICENSE Version 2'
                    url 'https://www.gnu.org/licenses/gpl-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'nonylene'
                    email 'cookie.speaker@gmail.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}
