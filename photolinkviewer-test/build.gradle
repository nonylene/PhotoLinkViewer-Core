apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "net.nonylene.photolinkviewer.core.test"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        def FLICKR_KEY
        def TUMBLR_KEY

        def file = project.file('key.properties')
        if (file.exists()) {
            def properties = new Properties()
            properties.load(file.newDataInputStream())
            FLICKR_KEY = properties.get("FLICKR_KEY")
            TUMBLR_KEY = properties.get("TUMBLR_KEY")
        } else {
            FLICKR_KEY = System.getenv("FLICKR_KEY")
            TUMBLR_KEY = System.getenv("TUMBLR_KEY")
        }

        buildConfigField "String", "FLICKR_KEY", "\"${FLICKR_KEY}\""
        buildConfigField "String", "TUMBLR_KEY", "\"${TUMBLR_KEY}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":photolinkviewer-core")
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    kapt "com.android.databinding:compiler:2.3.3"

    androidTestCompile('com.android.support.test:runner:0.4.1'){
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4.1'){
        exclude module: 'support-annotations'
    }
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

buildscript {
    ext.kotlin_version = '1.1.3'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}
